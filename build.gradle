plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'com.palantir.git-version' version '0.12.3'
}

group 'io.github.satosystems.ppap'
version gitVersion()

jar {
    manifest.mainAttributes('Main-Class': 'io.github.satosystems.ppap.Main')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    implementation 'net.lingala.zip4j:zip4j:2.9.1'
    implementation 'io.vavr:vavr:1.0.0-alpha-3'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

task zip(dependsOn: shadowJar) {
    doLast {
        def dir = new File("${rootDir}/.work")
        dir.deleteDir()
        dir.mkdirs()

        ant {
           ant.copy file: "build/libs/ppap-${version}-all.jar", tofile: "${dir}/ppap-${version}.jar", overwrite: true
        }

        copy {
            from 'script'
            include "ppap"
            into dir
        }

        exec {
            workingDir dir
            commandLine '/usr/bin/sed', '-I', '', "s/%VERSION%/${version}/g", 'ppap'
        }

        def out = new File("${rootDir}/out")
        out.mkdirs()

        exec {
            workingDir dir
            commandLine '/usr/bin/zip', "${out}/ppap-${version}.zip", 'ppap', "ppap-${version}.jar"
        }
    }
}
